#!/usr/bin/env zsh

# Enable parameter expansion, command substitution and arithmetic expansion in prompt string
setopt PROMPT_SUBST

# Inspired by https://github.com/necolas/dotfiles/blob/master/shell/bash_prompt
prompt_git() {
	! command git rev-parse --is-inside-work-tree &>/dev/null && return

	local s='';
	if [[ "$(command git rev-parse --is-inside-git-dir 2> /dev/null)" == "false" ]]; then
		command git update-index --really-refresh -q &>/dev/null;
		# [+] staged, uncommitted
		! command git diff --quiet --ignore-submodules --cached && s+='+'
		# [!] unstaged
		! command git diff-files --quiet --ignore-submodules -- && s+='!'
		# [?] untracked
		[ -n "$(command git ls-files --others --exclude-standard)" ] && s+='?'
		# [$] stashed
		command git rev-parse --verify refs/stash &>/dev/null && s+='$'
	fi
	[ -n "${s}" ] && s=" [${s}]"

	local branchName
  branchName="$(command git symbolic-ref --quiet --short HEAD 2> /dev/null || \
		command git rev-parse --short HEAD 2> /dev/null || \
		echo '(unknown)')"

	echo -e "${1}${branchName}${blue}${s}"
}

set_prompts() {
  if [ -x /usr/bin/tput ] && tput setaf 1 >&/dev/null; then
		tput sgr0; # reset colors
		reset=$(tput sgr0); blue=$(tput setaf 4); gray=$(tput setaf 8); green=$(tput setaf 2); red=$(tput setaf 1); violet=$(tput setaf 5); yellow=$(tput setaf 3);
    # Unused colors: bold=$(tput bold); black=$(tput setaf 0); cyan=$(tput setaf 6); orange=$(tput setaf 3); purple=$(tput setaf 5); white=$(tput setaf 15);
  fi

  # Highlight the user name when logged in as root.
  [[ "${USER}" == "root" ]] && userStyle="${red}" || userStyle="${yellow}"

  PROMPT="${userStyle}%n"; # username
  PROMPT+="${gray} in ";
  PROMPT+="${green}%(5~|%-1~/.../%3~|%4~)"; # working directory, https://unix.stackexchange.com/a/273567
  PROMPT+='$(prompt_git "${gray} on ${violet}")'; # git repository details
  PROMPT+=$'\n';
  PROMPT+="${reset}"'$ '; # `$` (and reset color)
  export PROMPT;
}
set_prompts
unset set_prompts

precmd() {
  if [ -z "${PROMPT_CTR}" ]; then
    PROMPT_CTR=1
  elif [ "${PROMPT_CTR}" -eq 1 ]; then
    echo ""
  fi
}
