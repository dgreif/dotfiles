#!/usr/bin/env zsh

# Enable parameter expansion, command substitution and arithmetic expansion in prompt string
setopt PROMPT_SUBST

# Inspired by https://github.com/necolas/dotfiles/blob/master/shell/bash_prompt
prompt_git() {
	! command git rev-parse --is-inside-work-tree &>/dev/null && return

	local s='';
	if [[ "$(command git rev-parse --is-inside-git-dir 2> /dev/null)" == "false" ]]; then
		command git update-index --really-refresh -q &>/dev/null;
		# [+] staged, uncommitted
		! command git diff --quiet --ignore-submodules --cached && s+='+'
		# [!] unstaged
		! command git diff-files --quiet --ignore-submodules -- && s+='!'
		# [?] untracked
		[ -n "$(command git ls-files --others --exclude-standard)" ] && s+='?'
		# [$] stashed
		command git rev-parse --verify refs/stash &>/dev/null && s+='$'
	fi
	[ -n "${s}" ] && s=" [${s}]"

	local branchName
  branchName="$(command git symbolic-ref --quiet --short HEAD 2> /dev/null || \
		command git rev-parse --short HEAD 2> /dev/null || \
		echo '(unknown)')"

	echo -e "${1}${branchName}%4F${s}"
}

set_prompts() {
  if [[ "${USER}" == "root" ]] || [[ "${USER}" == "pi" ]]; then
		PROMPT="%1F%n"
	else
		PROMPT="%3F%n"
	fi
  PROMPT+="%8F in ";
  PROMPT+="%2F%(5~|%-1~/.../%3~|%4~)"; # working directory, https://unix.stackexchange.com/a/273567
  PROMPT+='$(prompt_git "%8F on %5F")'; # git repository details
  PROMPT+=$'\n';
  PROMPT+="%f\$ "; # `$` (and reset color)
  export PROMPT;
}
set_prompts
unset set_prompts

precmd() {
  if [ -z "${PROMPT_CTR}" ]; then
    PROMPT_CTR=1
  elif [ "${PROMPT_CTR}" -eq 1 ]; then
    echo ""
		# osascript -e 'if app "Terminal" is frontmost then tell app "System Events" to keystroke "u" using command down'
  fi
}
