#!/usr/bin/env zsh
# shellcheck shell=sh

# COMPLETIONS
# Init zsh completions
autoload -U compinit
compinit

# Case-insensitive completion
# https://superuser.com/a/1092328
zstyle ':completion:*' matcher-list '' 'm:{a-zA-Z}={A-Za-z}'

# Menu selection
zstyle ':completion:*' menu yes select
zmodload zsh/complist
# Finish completion and execute with (return)
bindkey -M menuselect '^M' .accept-line
# Cancel selection with (esc)
bindkey -M menuselect '\e' send-break

# INPUT
# Use the string that has already been typed as the prefix for searching
# through commands (i.e. more intelligent Up/Down-arrow behavior)
bindkey "^[[A" history-beginning-search-backward
bindkey "^[OA" history-beginning-search-backward # SSH
bindkey "^[[B" history-beginning-search-forward
bindkey "^[OB" history-beginning-search-forward # SSH

# Reverse through the completions menu using shift-tab
bindkey "^[[Z" reverse-menu-complete

# Move cursor forward/backward by word (⌥→/⌥←)
bindkey "^[b" backward-word
bindkey "^[f" forward-word

# Fix backspace when existing vi mode
bindkey -v "^?" backward-delete-char

# Case-insensitive globbing (used in pathname expansion)
unsetopt CASE_GLOB

# Enable parameter expansion, command substitution and arithmetic expansion in prompt string
setopt PROMPT_SUBST

# PROMPT
# Inspired by https://github.com/necolas/dotfiles/blob/master/shell/bash_prompt
prompt_git() {
	! command git rev-parse --is-inside-work-tree &>/dev/null && return

	local s='';
	if [[ "$(command git rev-parse --is-inside-git-dir 2> /dev/null)" == "false" ]]; then
		command git update-index --really-refresh -q &>/dev/null;
		# [+] staged, uncommitted
		! command git diff --quiet --ignore-submodules --cached && s+='+'
		# [!] unstaged
		! command git diff-files --quiet --ignore-submodules -- && s+='!'
		# [?] untracked
		[ -n "$(command git ls-files --others --exclude-standard)" ] && s+='?'
		# [$] stashed
		command git rev-parse --verify refs/stash &>/dev/null && s+='$'
	fi
	[ -n "${s}" ] && s=" [${s}]"

	local branchName
  branchName="$(command git symbolic-ref --quiet --short HEAD 2> /dev/null || \
		command git rev-parse --short HEAD 2> /dev/null || \
		echo '(unknown)')"

	echo -e "${1}${branchName}%4F${s}"
}

set_prompts() {
	if [[ -n "${SSH_CLIENT}" ]]; then
		PROMPT="%3F%n%8F on %2F%m"
	else
		PROMPT="%3F%n"
	fi
  PROMPT+="%8F in ";
  PROMPT+="%2F%(5~|%-1~/.../%3~|%4~)"; # working directory, https://unix.stackexchange.com/a/273567
  PROMPT+='$(prompt_git "%8F on %5F")'; # git repository details
  PROMPT+=$'\n';
  PROMPT+="%f\$ "; # `$` (and reset color)
  export PROMPT;
}
set_prompts
unset set_prompts

precmd() {
  if [ -z "${PROMPT_CTR}" ]; then
    PROMPT_CTR=1
  elif [ "${PROMPT_CTR}" -eq 1 ]; then
    echo ""
		# osascript -e 'if app "Terminal" is frontmost then tell app "System Events" to keystroke "u" using command down'
  fi
}
